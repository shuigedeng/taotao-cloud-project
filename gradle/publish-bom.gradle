apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    repositories {

        //新的仓库
        maven {
            name = "AliyunNew"
            if (project.version.endsWith("-SNAPSHOT")) {
                url = "https://packages.aliyun.com/60516c4824e1e6fbecd22990/maven/2085441-snapshot-djviiu"
            } else {
                url = "https://packages.aliyun.com/60516c4824e1e6fbecd22990/maven/2085441-release-8v0qbs"
            }

            allowInsecureProtocol = true
            credentials {
                username = findProperty("mavenUsernameNew") ?: System.getenv("TAOTAO_CLOUD_MAVEN_USERNAME_NEW")
                password = findProperty("mavenPasswordNew") ?: System.getenv("TAOTAO_CLOUD_MAVEN_PASSWORD_NEW")
            }
        }

        maven {
            name = "GitHub"
            url = uri("https://maven.pkg.github.com/shuigedeng/taotao-cloud-project")
            credentials {
                username = findProperty("githubUsername") ?: System.getenv("TAOTAO_CLOUD_GITHUB_USERNAME")
                password = findProperty("githubPassword") ?: System.getenv("TAOTAO_CLOUD_GITHUB_TOKEN")
            }
        }

        maven {
            name = "Sonatype"
            if (project.version.endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            allowInsecureProtocol = true
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("TAOTAO_CLOUD_OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("TAOTAO_CLOUD_OSSRH_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version

            //打包类型 war: components.web jar: components.java
            from components.javaPlatform

            pom {
                name = project.name
                description = project.name
                url = "https://github.com/shuigedeng/taotao-cloud-project.git"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "shuigedeng"
                        name = "shuigedeng"
                        email = "981376577@qq.com"
                    }
                }

                scm {
                    connection = "scm:git:git@github.com:shuigedeng/taotao-cloud-project.git"
                    developerConnection = "scm:git:ssh://git@github.com:shuigedeng/taotao-cloud-project.git"
                    url = "https://github.com/shuigedeng/taotao-cloud-project.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
